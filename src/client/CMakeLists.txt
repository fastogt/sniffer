SET(CLIENT_NAME ${PROJECT_NAME}_client)
SET(CLIENT_NAME_EXE ${CLIENT_NAME}_s)

SET(GLOBAL_HEADERS
  ${CMAKE_CURRENT_SOURCE_DIR}/sniffer_service.h
  ${CMAKE_CURRENT_SOURCE_DIR}/config.h
)
SET(GLOBAL_SOURCES
  ${CMAKE_CURRENT_SOURCE_DIR}/sniffer_service.cpp
  ${CMAKE_CURRENT_SOURCE_DIR}/config.cpp
)

# HARDWARE specific
IF(NOT DEFINED HARDWARE_LICENSE_ALGO)
  SET(HARDWARE_LICENSE_ALGO 0)
ENDIF(NOT DEFINED HARDWARE_LICENSE_ALGO)

SET(LICENSE_KEY "" CACHE STRING "Hardware specific key")
MESSAGE(STATUS "LICENSE_KEY: ${LICENSE_KEY}")

# platform
IF(APPLE)
  SET(PLATFORM_HEADER)
  SET(PLATFORM_SOURCES)
  SET(PLATFORM_LIBRARIES)
ELSEIF(UNIX)
  SET(PLATFORM_HEADER)
  SET(PLATFORM_SOURCES)
  SET(PLATFORM_LIBRARIES rt m z pthread)
ENDIF(APPLE)

FIND_PACKAGE(Common REQUIRED)
FIND_PACKAGE(JSON-C REQUIRED)
IF(NOT TARGET inih)
  ADD_SUBDIRECTORY(${CMAKE_SOURCE_DIR}/src/third-party/ini inih)
ENDIF(NOT TARGET inih)

SET(PRIVATE_INCLUDE_DIRECTORIES_CLIENT
  ${PRIVATE_INCLUDE_DIRECTORIES_CLIENT}
  ${CMAKE_SOURCE_DIR}/src
  ${COMMON_INCLUDE_DIRS}
  ${JSONC_INCLUDE_DIRS}
  ${CMAKE_SOURCE_DIR}/src/third-party/ini
  ${CMAKE_SOURCE_DIR}/src/third-party/sds
)

SET(RUN_DIR_PATH "/var/run/${CLIENT_NAME}")
SET(PIDFILE_PATH "${RUN_DIR_PATH}/${CLIENT_NAME}.pid")
SET(USER_NAME ${PROJECT_NAME_LOWERCASE})
SET(USER_GROUP ${PROJECT_NAME_LOWERCASE})
SET(CONFIG_FILE_PATH "/etc/${CLIENT_NAME}.conf")

SET(PRIVATE_COMPILE_DEFINITIONS_CLIENT ${PRIVATE_COMPILE_DEFINITIONS_CLIENT}
  -DHARDWARE_LICENSE_ALGO=${HARDWARE_LICENSE_ALGO}
  -DUSER_NAME="${USER_NAME}"
  -DUSER_GROUP="${USER_GROUP}"
  -DCONFIG_FILE_PATH="${CONFIG_FILE_PATH}"
  -DLICENSE_KEY="${LICENSE_KEY}"
  -DPIDFILE_PATH="${PIDFILE_PATH}"
  -DCORE_LIBRARY="${CORE_LIBRARY}"
  -DPIDFILE_PATH="${PIDFILE_PATH}"
  -DSERVICE_NAME="${CLIENT_NAME}"
  -DRELATIVE_SOURCE_DIR="${RELATIVE_SOURCE_DIR}"
)

SET(CLIENT_SOURCES ${CLIENT_SOURCES}
  ${GLOBAL_HEADERS} ${GLOBAL_SOURCES}
)

SET(CLIENT_LIBRARIES ${CLIENT_LIBRARIES}
  ${SNIFFER_COMMON}
  ${COMMON_LIBRARIES}
  ${JSONC_LIBRARIES} inih pcap udev ${PLATFORM_LIBRARIES}
)

ADD_EXECUTABLE(${CLIENT_NAME_EXE} ${CLIENT_SOURCES} main.cpp ${SDS_SOURCES})
TARGET_INCLUDE_DIRECTORIES(${CLIENT_NAME_EXE} PRIVATE ${PRIVATE_INCLUDE_DIRECTORIES_CLIENT})
TARGET_COMPILE_DEFINITIONS(${CLIENT_NAME_EXE} PRIVATE ${PRIVATE_COMPILE_DEFINITIONS_CLIENT})
TARGET_LINK_LIBRARIES(${CLIENT_NAME_EXE} ${CLIENT_LIBRARIES})

IF(PROJECT_BUILD_TYPE_VERSION STREQUAL "release")
  STRIP_TARGET(${CLIENT_NAME_EXE})
ENDIF(PROJECT_BUILD_TYPE_VERSION STREQUAL "release")
INSTALL(TARGETS ${CLIENT_NAME_EXE} DESTINATION ${TARGET_INSTALL_DESTINATION} COMPONENT APPLICATIONS)

SET(EXECUTABLE_FOLDER_PATH /usr/local/bin)

IF(OS_WINDOWS)
ELSEIF(OS_MACOSX)
ELSEIF(OS_LINUX)
  SET(EXECUTABLE_PATH ${EXECUTABLE_FOLDER_PATH}/${CLIENT_NAME} CACHE INTERNAL "Daemon path: ${EXECUTABLE_PATH}")
ENDIF(OS_WINDOWS)

# service
SET(CLIENT_SCRIPT_GEN_PATH ${CMAKE_BINARY_DIR}/client/${CLIENT_NAME}.service)
GEN_SERVICE_SERVICE_FILE(${CLIENT_SCRIPT_GEN_PATH}
  ${CLIENT_NAME} ${CLIENT_NAME_EXE}
  ${EXECUTABLE_PATH}
  ${RUN_DIR_PATH}
  ${PIDFILE_PATH}
  ${USER_NAME} ${USER_GROUP}
  ${PROJECT_SUMMARY}
)
INSTALL(FILES ${CLIENT_SCRIPT_GEN_PATH} DESTINATION /etc/systemd/system/)

SET(CLIENT_DEB_SCRIPT_GEN_PATH ${CMAKE_BINARY_DIR}/client/${CLIENT_NAME}.debian)
GEN_DEBIAN_SERVICE_FILE(${CLIENT_DEB_SCRIPT_GEN_PATH}
  ${CLIENT_NAME} ${CLIENT_NAME_EXE}
  ${EXECUTABLE_PATH}
  ${RUN_DIR_PATH}
  ${PIDFILE_PATH}
  ${USER_NAME} ${USER_GROUP}
  ${PROJECT_SUMMARY}
)
INSTALL(FILES ${CLIENT_DEB_SCRIPT_GEN_PATH}
  DESTINATION /etc/init.d
  RENAME ${CLIENT_NAME}
  PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE GROUP_READ GROUP_EXECUTE WORLD_READ WORLD_EXECUTE)

SET(CLIENT_CONF_GEN_PATH ${CMAKE_BINARY_DIR}/client/${CLIENT_NAME}.conf)
CONFIGURE_FILE(${CMAKE_SOURCE_DIR}/install/${PROJECT_NAME_LOWERCASE}/client/service.conf.in
  ${CLIENT_CONF_GEN_PATH} @ONLY IMMEDIATE
)
IF(NOT EXISTS ${CONFIG_FILE_PATH})
  INSTALL(FILES ${CLIENT_CONF_GEN_PATH} DESTINATION /etc/)
ENDIF(NOT EXISTS ${CONFIG_FILE_PATH})

# exe script
SET(CLIENT_START_SCRIPT_GEN_PATH ${CMAKE_BINARY_DIR}/client/${CLIENT_NAME})
GEN_START_SCRIPT(${CLIENT_START_SCRIPT_GEN_PATH} ${CLIENT_NAME})
INSTALL(PROGRAMS ${CLIENT_START_SCRIPT_GEN_PATH} DESTINATION ${TARGET_INSTALL_DESTINATION} COMPONENT APPLICATIONS)

# install other
INSTALL(FILES ${CMAKE_SOURCE_DIR}/LICENSE DESTINATION . COMPONENT LICENSE RENAME LICENSE OPTIONAL)
INSTALL(FILES ${CMAKE_SOURCE_DIR}/COPYRIGHT DESTINATION . COMPONENT LICENSE RENAME COPYRIGHT OPTIONAL)
INSTALL(FILES ${PROJECT_CHANGELOG_FILE} DESTINATION . COMPONENT LICENSE RENAME CHANGELOG OPTIONAL)

# developer

IF (DEVELOPER_CHECK_STYLE)
  SET(CHECK_SOURCES ${CLIENT_SOURCES})
  REGISTER_CHECK_STYLE_TARGET(check_style_${CLIENT_NAME} "${CHECK_SOURCES}")
  REGISTER_CHECK_INCLUDES_TARGET(${CLIENT_NAME})
ENDIF(DEVELOPER_CHECK_STYLE)

IF(DEVELOPER_ENABLE_TESTS)

ENDIF(DEVELOPER_ENABLE_TESTS)

